#
# Copyright 2010-2014 Fabric Software Inc. All rights reserved.
#
import platform, json, os, shutil
Import('parentEnv', 'kl2edk', 'kl', 'extSuffix', 'STAGE_DIR', 'LEAP_LIB_DIR')

extName = 'LeapMotion'

env = parentEnv.Clone()
env.Append(CPPPATH = [env.Dir('.').abspath])
env.Append(CPPPATH = [env.Dir('thirdparty').abspath])
env.Append(LIBS = ['Leap'])
env.Append(CCFLAGS = ['/EHsc'])

sources = [
  env.File('LeapMotion.fpm.json')
]
sources += env.Glob('*.kl')

cppFiles = [
  env.File('extension.cpp'),
  env.File('LeapConfig_functions.cpp'),
  env.File('LeapDevice_functions.cpp'),
  env.File('LeapImage_functions.cpp'),
  env.File('LeapArm_functions.cpp'),
  env.File('LeapBone_functions.cpp'),
  env.File('LeapFinger_functions.cpp'),
  env.File('LeapTool_functions.cpp'),
  env.File('LeapHand_functions.cpp'),
  env.File('LeapPointable_functions.cpp'),
  env.File('LeapGesture_functions.cpp'),
  env.File('LeapInteractionBox_functions.cpp'),
  env.File('LeapFrame_functions.cpp'),
  env.File('LeapController_functions.cpp')
]

headers = [
  env.File('LeapMotion.h')
]

extData = json.loads(open(sources[0].srcnode().abspath, 'r').read())
extVersion = extData['version']

extensionFiles = env.Install(STAGE_DIR, [env.File(extName+'.fpm.json')] + env.Glob('*.kl'))
kl2edkResults = env.RunKL2EDK(headers, sources)

extLibFileName = env.File(extName + '-' + extSuffix)
libraryFiles = Flatten([env.SharedLibrary(extLibFileName, cppFiles)])
env.Depends(libraryFiles, kl2edkResults)

extensionFiles += env.Install(STAGE_DIR, libraryFiles[0])
extensionFiles = Flatten(extensionFiles)

if platform.system().lower().startswith('win'):
  for lib in Glob(os.path.join(LEAP_LIB_DIR, '*.dll')):
    extensionFiles +=  env.Install(STAGE_DIR, lib)

if os.environ.has_key('FABRIC_EXTS_INSTALL_DIR'):
  INSTALL_DIR = os.environ['FABRIC_EXTS_INSTALL_DIR']
  INSTALL_DIR = os.path.normpath(os.path.join(INSTALL_DIR, extName, extVersion))

  newExtensionFiles = []
  for sourceFile in extensionFiles:
    relPath = os.path.relpath(sourceFile.abspath, STAGE_DIR.abspath)
    targetPath = os.path.join(INSTALL_DIR, relPath)
    targetFile = env.File(targetPath)
    newExtensionFiles += env.Install(targetFile.dir, sourceFile)
  extensionFiles += Flatten(newExtensionFiles)

env.AlwaysBuild(extensionFiles)
alias = env.Alias(extName, extensionFiles)

Return('alias')
